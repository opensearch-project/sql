/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java-library'
    id "io.freefair.lombok"
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.9.0'
}

dependencies {
    implementation project(':core')
    implementation project(':common')
    runtimeOnly('org.apache.hive:hive-jdbc:3.1.3') {
        exclude group: "org.apache.hadoop", module: "hadoop-common"
        exclude group: 'org.apache.zookeeper'
        exclude group: "org.apache.hive", module: "hive-metastore"
        exclude group: "org.apache.hive", module: "hive-shims"
        exclude group: 'org.apache.hive', module: 'hive-llap-server'
        exclude group: 'org.apache.hive', module: 'hive-upgrade-acid'

        // exclude for resolving version conflict
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'

        // exclude for include with transitive = false
        exclude group: "org.apache.hive", module: "hive-common"
        exclude group: "org.apache.hive", module: "hive-service"
        exclude group: "org.apache.hive", module: "hive-serde"
        exclude group: 'org.apache.hive', module: 'hive-service-rpc'

        // exclude because of CVE-2019-0205
        exclude group: 'org.apache.thrift', module: 'libthrift'
    }
    runtimeOnly('org.apache.httpcomponents:httpcore')
    constraints {
        runtimeOnly('org.apache.httpcomponents:httpcore:4.4.12')
    }
    runtimeOnly('org.apache.httpcomponents:httpclient')
    constraints {
        runtimeOnly('org.apache.httpcomponents:httpclient:4.5.13')
    }
    runtimeOnly('org.apache.hive:hive-service:3.1.3') {
        transitive = false
    }
    runtimeOnly('org.apache.hive:hive-serde:3.1.3') {
        transitive = false
    }
    runtimeOnly('org.apache.hive:hive-common:3.1.3') {
        transitive = false
    }
    runtimeOnly('org.apache.hive:hive-service-rpc:3.1.3') {
        transitive = false
    }
    runtimeOnly('org.apache.thrift:libthrift:0.18.1')
    runtimeOnly 'commons-lang:commons-lang:2.6'

    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.12.4'
}

pitest {
    targetClasses = ['org.opensearch.sql.*']
    pitestVersion = '1.9.0'
    threads = 4
    outputFormats = ['HTML', 'XML']
    timestampedReports = false
    junit5PluginVersion = '1.0.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
            ]
            limit {
                counter = 'LINE'
                minimum = 1.0
            }
            limit {
                counter = 'BRANCH'
                minimum = 1.0
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport
