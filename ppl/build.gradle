/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java'
    id "io.freefair.lombok"
    id 'jacoco'
    id 'antlr'
}

generateGrammarSource {
    arguments += ['-visitor', '-package', 'org.opensearch.sql.ppl.antlr.parser']
    source = sourceSets.main.antlr
    outputDirectory = file("build/generated-src/antlr/main/org/opensearch/sql/ppl/antlr/parser")
}

configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7.1"

    compile "org.antlr:antlr4-runtime:4.7.1"
    compile group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'
    compile group: 'org.opensearch', name: 'opensearch-x-content', version: "${opensearch_version}"
    compile group: 'org.json', name: 'json', version: '20231013'
    compile group: 'org.springframework', name: 'spring-context', version: "${spring_version}"
    compile group: 'org.springframework', name: 'spring-beans', version: "${spring_version}"
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version:'2.17.1'
    compile project(':common')
    compile project(':core')
    compile project(':protocol')

    testCompile group: 'junit', name: 'junit', version: '4.13.2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.3.3'

}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/antlr/parser/**'])
        }))
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }

        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/antlr/parser/**'])
        }))
    }
}
check.dependsOn jacocoTestCoverageVerification
