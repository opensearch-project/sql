/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java-library'
    id "io.freefair.lombok"
    id 'jacoco'
}

ext {
    hadoop = "3.3.4"
    aws = "1.12.330"
}

configurations.all {
    resolutionStrategy.force "commons-io:commons-io:2.8.0"
}

dependencies {
    implementation project(':core')
    // required by hadoop filesystem https://hadoop.apache.org/docs/stable/hadoop-project-dist/hadoop-common/filesystem/index.html.
    implementation("org.apache.hadoop:hadoop-common:${hadoop}") {
        exclude group: 'org.apache.zookeeper'
        exclude group: 'org.eclipse.jetty'
        exclude group: 'com.sun.jersey'
        exclude group: 'javax.servlet.jsp'
        exclude group: 'javax.servlet'
        exclude group: 'org.apache.kerby'
        exclude group: 'org.apache.curator'
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
        exclude group: 'org.apache.avro', module: 'avro'
        exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
        // enforce version.
        exclude group: 'com.fasterxml.woodstox', module: 'woodstox-core'
        exclude group: 'commons-io', module: 'commons-io'
        exclude group: 'ch.qos.reload4j', module: 'reload4j'
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
    }
    implementation('com.fasterxml.woodstox:woodstox-core')
    constraints {
        implementation('com.fasterxml.woodstox:woodstox-core:6.4.0') {
            because 'https://www.mend.io/vulnerability-database/CVE-2022-40156'
        }
    }
    implementation('commons-io:commons-io')
    constraints {
        implementation('commons-io:commons-io:2.8.0') {
            because 'between versions 2.8.0 and 2.5'
        }
    }
    implementation('ch.qos.reload4j:reload4j')
    constraints {
        implementation('ch.qos.reload4j:reload4j:1.2.22') {
            because 'between versions 1.2.22 and 1.2.19'
        }
    }
    implementation('org.apache.httpcomponents:httpcore')
    constraints {
        implementation('org.apache.httpcomponents:httpcore:4.4.15') {
            because 'between versions 4.4.15 and 4.4.13'
        }
    }

    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.12.4'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }

    // hadoop-fs depend on native library which is missing on windows.
    // https://hadoop.apache.org/docs/r3.3.4/hadoop-project-dist/hadoop-common/NativeLibraries.html#Native_Hadoop_Library
    if ('windows' == project.getProperties().getOrDefault('buildPlatform', 'linux')) {
        excludes = [
                '**/FileSystemStreamSourceTest.class'
        ]
    }
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)

jacocoTestCoverageVerification {
    violationRules {
        rule {
            // hadoop-fs depend on native library which is missing on windows.
            // https://hadoop.apache.org/docs/r3.3.4/hadoop-project-dist/hadoop-common/NativeLibraries.html#Native_Hadoop_Library
            if ('windows' == project.getProperties().getOrDefault('buildPlatform', 'linux')) {
                excludes = [
                        'org.opensearch.sql.filesystem.streaming.FileSystemStreamSource'
                ]
            }
            element = 'CLASS'
            limit {
                counter = 'LINE'
                minimum = 1.0
            }
            limit {
                counter = 'BRANCH'
                minimum = 1.0
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport
