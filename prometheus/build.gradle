/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java-library'
    id "io.freefair.lombok"
    id 'jacoco'
}

dependencies {
    api project(':core')
    api group: 'org.opensearch', name: 'opensearch', version: "${opensearch_version}"
    implementation "io.github.resilience4j:resilience4j-retry:1.5.0"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "${jackson_version}"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jackson_databind_version}"
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-cbor', version: "${jackson_version}"
    compileOnly group: 'org.opensearch.client', name: 'opensearch-rest-high-level-client', version: "${opensearch_version}"
    api group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'
    implementation group: 'org.json', name: 'json', version: '20180813'

    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.12.4'
    testImplementation group: 'org.opensearch.client', name: 'opensearch-rest-high-level-client', version: "${opensearch_version}"
    testImplementation group: 'org.opensearch.test', name: 'framework', version: "${opensearch_version}"
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.9.3'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

configurations.all {
    resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib:1.6.0"
    resolutionStrategy.force "org.jetbrains.kotlin:kotlin-stdlib-common:1.6.0"
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                    'org.opensearch.sql.prometheus.data.constants.*'
            ]
            limit {
                counter = 'LINE'
                minimum = 1.0
            }
            limit {
                counter = 'BRANCH'
                minimum = 1.0
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport
